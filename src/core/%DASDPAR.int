ROUTINE %DASDPAR [Type=INT,LanguageMode=6]
%DASDPAR	;;4-Mar-92;V6.1
	;;     Copyright ゥ Digital Equipment Corporation.
	;;       1987 - 1992  All rights reserved.
	;	%LINE   - Line of DASL Commands
	;	%COM	- Keyword for a singel command
	;	%OPCODE	- 2 letter code for %COM, compiler
	;	%PCOND	- Optional post-conditional of the form 'EXPR'
	;	%PARA	- Command Qualifiers,parameters and/or arguments.
	; 	%TYP	- Indicates where command may be used.
	;	%LAB	- Line label fo validation subroutine
	;	%UCRES	- %RES converted to uppercase
	;	%END	- pointer to last command character of %RES
PLINESC	;
	N F,%SP,SLA
	S F=$P(%W,";",7)
	S %SP=$S(F="ACT1":1,F="ACT2":2,F="ACT3":3)
	S SLA(1)="2;0"
	S SLA(1,"SDFACT")=SDACT1
	S SLA(2)="3;1"
	S SLA(2,"SDFACT")=SDACT2
	S SLA(3)="4;2"
	S SLA(3,"SDFACT")=SDACT3
PLINE	;
	N SP,Q,C,PREL
	S SP=%SP
	S %LINE=%RES
	I $E(%LINE,$L(%LINE))="-" S %LINE=$E(%LINE,1,$L(%LINE)-1)
PREC	;
PRELOOP	;
	I $P(SLA(SP),";",2)=0 G PLINE3
	S PREL=$$UPCASEQ^%DAZCALL(SLA($P(SLA(SP),";",2),"SDFACT"))
	I $E(PREL,$L(PREL))'="-" G PLINE3
	I $L(%LINE)+$L(PREL)>240 S %MSG="継続行の compined length が長すぎます。" Q  ;;%%Z3-T
	S %LINE=$E(PREL,1,$L(PREL)-1)_%LINE,SP=$P(SLA(SP),";",2) G PRELOOP
	Q
PLINEC	;
	S %END=0,S=""
	F %COUNT=1:1 Q:%LINE=""  D PONE I %MSG]"" G PCOMPER
	Q
PLINE2	;
	S (%UCRES,%LINE)=$$UPCASEQ^%DAZCALL(%RES)
PLINE3	S %END=0
	S %UCRES=$$UPCASEQ^%DAZCALL(%RES)
	S %LINE=$$UPCASEQ^%DAZCALL(%LINE)
	S %LINEL=$L(%LINE)
	F %COUNT=1:1 Q:%LINE=""  D PONE I %MSG]"" G PKILL
	I %END S %END=%END-(%LINEL-$L(%RES)),%RES=$E(%UCRES,1,%END)_$E(%RES,%END+1,255)
PKILL	K J,K,L,N,Q,%PRC,%QC,%X1,%X2,%X3,%X4,%X5,%END,%UCRES,%LINEL
	K %COUNT,%DUM,%OPCODE,%PCOND,%LINE,%TYP,%LAB,%COM,%PARA
	K %POPX,%POPY,%POPW,%POPF,%POPA ;	;%%Z2-X7
	Q
PONE	;
	F J=1:1 Q:$E(%LINE,J)'=" "
	I $E(%LINE,J)="!" D  S %LINE="" Q
	. I $E(%LINE,J,J+1)="!!" S S="{CM}"_$E(%LINE,J+2,999)
	S N=J S:$E(%LINE,N)="%" J=J+1 F J=J:1 Q:$E(%LINE,J)'?1UN
	S %COM=$E(%LINE,N,J-1),%END=%END+J-1
	I ",CO,COND,PA,PATTERN,XE,XECUTE,DF,DEFAULT,"[(","_%COM_","),$E(%LINE,J)'=" " S %MSG=%COM_"コマンドの後部の文字"""_$E(%LINE,J)_"""が不適当です。" Q  ;;%%Z1-T
	I %COM="" S %MSG="コマンドが不適当です。" Q  ;;%%Z1-T
	I "%"[%COM S %MSG="コマンドナンバー"_%COUNT_"にキーワードがありません。" Q  ;;%%Z1-T
	I $E(%COM)="%","CA"[$E(%TYP,3) D PUSRCOM G PARG:%MSG="" Q
	S %X1=$T(@%COM)
	I $P(%X1,";",3)'[%TYP S %MSG="キーワード'"_%COM_"'が不適当です。" Q  ;;%%Z1-T
	S %OPCODE=$E($P(%X1,%TYP,2),1,2),%LAB=$P(%X1,";",4),%PARA="",%PCOND=""
PARG	F J=J:1 Q:$E(%LINE,J)'=" "  S %END=%END+1
	S %LINE=$E(%LINE,J,255),J=1,%END=%END+1
	I %LAB]"" D @%LAB Q:%MSG]""  S %END=%END+J-1
	F J=J:1 Q:$E(%LINE,J)'=" "  S %END=%END+1
	S L=$E(%LINE,J,255),%LINE=$P(L,";",2,99),L=$P(L,";",1)
	I L]"" S %MSG=%COM_"コマンドの後部の文字 """_L_""" が分かりません。" Q  ;;%%Z1-T
	I $D(S) S S=S_"{"_%OPCODE_%PCOND_"}"_%PARA
	I $D(S),$G(DB)=2 U 0 W !,S,!
	Q
PUSRCOM	;
	I %COM'?1"%"1U.6UN S %MSG="コマンド名 "_%COM_" が不適当です。" Q  ;;%%Z1-T
	I '$D(^DASDC($E(%COM,2,9))) S %MSG="コマンド名 "_%COM_" が未定義です。" Q  ;;%%Z1-T
	S %OPCODE="ZZ",%PARA=%COM,%PCOND="",%LAB=$S($E(%TYP,3)="C":"GSCND^%DASDP1",1:"")
	Q
PCOMPER	;
	S %MSG=$S(F="%DAT":"データネーム'"_%DDNM_"'",F=0:"スクリーン定義",1:"フィールド'"_F_"'")_" - "_%MSG D PKILL  ;;%%Z1-T
	ZT %MSG
ALL	;
	S %MSG="",SDFL=%SDFL,SDLL=%SDLL
	S F=0 D ALLS
	S F=0 F %=0:0 S F=$O(F0(F)) Q:F=""  D ALLF
	I %SDTYPE="DATA" S %DDNM="",F="%DAT" F %=0:0 S %DDNM=$O(%DN(%DDNM)) Q:%DDNM=""  I %DN(%DDNM,"AT")]"" D ALLD
	D PKILL
	Q
ALLS	;
	S %TYP=",ST"
	F %=1:1 Q:'$D(^DASD(%SDNM,"D","S","AT",%))  S %LINE=^DASD(%SDNM,"D","S","AT",%) I %LINE'=""  D PLINEC S FT(0,%)=S
	Q:%SDTYPE="OPTION"
	S ACTLIN=1,FC(F,1)=""
	S %LINE=""
	F I=1:1:3 S %=^DASD(%SDNM,"D","S","AC",I) Q:%=""  S %LINE=$E(%LINE,1,$L(%LINE)-1)_^(I),%TYP=",FC" I $E(%LINE,$L(%LINE))'="-" D ACTLIN S %LINE=""
	Q
ALLF	;
	S %TYP=",FT",%LINE=^DASD(%SDNM,"D","F",F,"AT",1) D PLINEC S FT(F)=S
	Q:%SDTYPE="OPTION"
	I $D(^DASD(%SDNM,"D","F",F,"AC")) S ACTLIN=1,FC(F,1)="" S %LINE="" F %=0:0 S %=$O(^DASD(%SDNM,"D","F",F,"AC",%)) Q:%=""  S %LINE=$E(%LINE,1,$L(%LINE)-1)_^(%),%TYP=",FC" I $E(%LINE,$L(%LINE))'="-"  D ACTLIN S %LINE=""
	S %TYP=",FA",%LINE=FV(F) D PLINEC S FV(F)=S
	Q
ALLD	;
	S %TYP=",DT",%LINE=%DN(%DDNM,"AT") D PLINEC S %DN(%DDNM,"AT")=S
	Q
ALLO	;
	S %TYP=",OC"
	F %SDON=0:0 S %SDON=$O(^DASD(%SDNM,"O","O",%SDON)) Q:%SDON=""  I $D(^(%SDON,"AC")) D ALLO1
	D PKILL
	Q
ALLO1	;
	S F=%SDON,ACTLIN=1,FC(F,1)=""
	F %=0:0 S %=$O(^DASD(%SDNM,"O","O",F,"AC",%)) Q:%=""  S %LINE=^(%) D ACTLIN
	Q
ACTLIN	;
	D PLINEC
	;**TM**;S %KJ=$ZC(KJOFF)  ;;%%Z1-A
	;**TM**;I $L(S)+$L(FC(F,ACTLIN))>255 S %KJ=$ZC(KJON) S ACTLIN=ACTLIN+1,FC(F,ACTLIN)=S Q  ;;%%Z1-A
	;**TM**;S %KJ=$ZC(KJON)  ;;%%Z1-A
	I $L(S)+$L(FC(F,ACTLIN))>255 S ACTLIN=ACTLIN+1,FC(F,ACTLIN)=S Q  ;;%%Z1-A
	S FC(F,ACTLIN)=FC(F,ACTLIN)_S
	Q
VST	S %TYP=",ST" G PLINE2
VSC	S %TYP=",SC" G PLINESC
VFT	S %TYP=",FT" G PLINE2
VFC	S %TYP=",FC" G PLINE
VFA	S %TYP=",FA" G PLINE2
VOC	S %TYP=",OC" G PLINE2
VDA	S %TYP=",DA" G PLINE2
VDT	S %TYP=",DT" G PLINE2
132	;;,ST13
ASSIGN	;;,SCAS,FCAS;LASSIGN^%DASDP1
AS	;;,SCAS,FCAS;LASSIGN^%DASDP1
BATCH	;;,STBA;LBATCH^%DASDP1
BLINK	;;,FTBL
BL	;;,FTBL
BOLD	;;,FTBO
BO	;;,FTBO
BOX	;;,STBX;LBOX^%DASDP1
CHECKPT	;;,STCP
CP	;;,STCP
COND	;;,SCCO,FCCO,FACO,DACO;LCOND^%DASDP1
CO	;;,SCCO,FCCO,FACO,DACO;LCOND^%DASDP1
COUNT	;;,SCCN,FCCN;LCOUNT^%DASDP1
CN	;;,SCCN,FCCN;LCOUNT^%DASDP1
DATE	;;,SCDT,FCDT,FADT,DADT;LDATE^%DASDP1
DT	;;,SCDT,FCDT,FADT,DADT;LDATE^%DASDP1
DATEN	;;,SCDN,FCDN,FADN,DADN;LDATE^%DASDP1
DTN	;;,SCDN,FCDN,FADN,DADN;LDATE^%DASDP1
DEFAULT	;;,FTDF;LDFAULT^%DASDP1
DF	;;,FTDF;LDFAULT^%DASDP1
DEMAND	;;,FTDM
DM	;;,FTDM
DISPLAY	;;,SCDS,FCDS;LDISP^%DASDP1
DS	;;,SCDS,FCDS;LDISP^%DASDP1
DO	;;,SCDO,FCDO,FADO,DADO,OCDO;LDO^%DASDP1
DONP	;;,SCDP,FCDP;LDO^%DASDP1
EDIT	;;,STED,FTED,DTED;;	;;%%Z1-Q
ED	;;,STED,FTED,DTED;;	;;%%Z1-Q
ERASE	;;,FCER,SCER;LERASE^%DASDP1
ERS	;;,FCER,SCER;LERASE^%DASDP1
ERROR	;;,FCRR;LERROR^%DASDP1
ERR	;;,FCRR;LERROR^%DASDP1
EVAL	;;,SCEV,FCEV;LEVAL^%DASDP1
EV	;;,SCEV,FCEV;LEVAL^%DASDP1
EVALS	;;,SCES,FCES;LEVALS^%DASDP1
EVS	;;,SCES,FCES;LEVALS^%DASDP1
EXIT	;;,STEX,SCEX,FTEX,FCEX;LEXIT^%DASDP1
EX	;;,STEX,SCEX,FTEX,FCEX;LEXIT^%DASDP1
FILE	;;,FCFI;LFILE^%DASDP1
FI	;;,FCFI;LFILE^%DASDP1
FILES	;;,FCFS;LFILES^%DASDP1
FIS	;;,FCFS;LFILES^%DASDP1
FKEY	;;,STFK,FTFK;LFKEY^%DASDP1;	;%%Z2-X6
FK	;;,STFK,FTFK;LFKEY^%DASDP1;	;%%Z2-X6
FULL	;;,DTFU,FTFU
FL	;;,DTFU,FTFU
GRAPHIC	;;,FTGR;LGRAPH^%DASDP1;		;%%Z3-COLOR
GR	;;,FTGR;LGRAPH^%DASDP1;		;%%Z3-COLOR
HANG	;;,SCWT,FCWT;LHANG^%DASDP1
HA	;;,SCWT,FCWT;LHANG^%DASDP1
INTEGER	;;,SCIN,FCIN,FAIN,DAIN;LINT^%DASDP1
IN	;;,SCIN,FCIN,FAIN,DAIN;LINT^%DASDP1
JNAME	;;,SCJN,FCNJ,FAJN,DAJN;;	;;%%Z1-M
JNM	;;,SCJN,FCJN,FAJN,DAJN;;	;;%%Z1-M
KEY	;;,FTKY;LKEY^%DASDP1
KILL	;;,SCKL,FCKL,OCKL;LKILL^%DASDP1
KL	;;,SCKL,FCKL,OCKL;LKILL^%DASDP1
LCASE	;;,FTLC,DTLC
LC	;;,FTLC,DTLC
LCASEQ	;;,FTLQ,DTLQ
LQ	;;,FTLQ,DTLQ
LOCK	;;,SCLK,FCLK;LLOCK^%DASDP1
LK	;;,SCLK,FCLK;LLOCK^%DASDP1
LOG	;;,SCLO,FCLO;LLOG^%DASDP1
LOGDMP	;;,SCLD,FCLD
LOGICAL	;;,SCYN,FCYN,FAYN,DAYN
LGC	;;,SCYN,FCYN,FAYN,DAYN
LOGOFF	;;,SCLF,FCLF
LF	;;,SCLF,FCLF
LOGON	;;,SCLN,FCLN
LN	;;,SCLN,FCLN
LOOKUP	;;,FCLU,SCLU,FALU,DALU;LLOOKUP^%DASDP1
LU	;;,FCLU,SCLU,FALU,DALU;LLOOKUP^%DASDP1
MUMPS	;;,SCMU,FCMU,OCMU;LMUMPS^%DASDP1
MU	;;,SCMU,FCMU,OCMU;LMUMPS^%DASDP1
NAME	;;,SCNM,FCNM,FANM,DANM
NM	;;,SCNM,FCNM,FANM,DANM
NOBACK	;;,FTNB,FCNB;LNOBACK^%DASDP1
NB	;;,FTNB,FCNB;LNOBACK^%DASDP1
NOECHO	;;,FTNE,DTNE
NE	;;,FTNE,DTNE
NOFILTER	;;,FTNR,DTNR
NOF	;;,FTNR,DTNR
NOPAD	;;,STQS,FTQF
NP	;;,STQS,FTQF
NUMERIC	;;,SCNU,FCNU,FANU,DANU;LNUM^%DASDP1
NU	;;,SCNU,FCNU,FANU,DANU;LNUM^%DASDP1
NXTFLD	;;,SCNF,FCNF;LNXTFLD^%DASDP1
NF	;;,SCNF,FCNF;LNXTFLD^%DASDP1
NXTSCN	;;,SCNS,FCNS;LNXTSCN^%DASDP1
NS	;;,SCNS,FCNS;LNXTSCN^%DASDP1
PATTERN	;;,SCPA,FCPA,FAPA,DAPA;LPATT^%DASDP1
PA	;;,SCPA,FCPA,FAPA,DAPA;LPATT^%DASDP1
PERM	;;,STPM
PM	;;,STPM
POPUP	;;,SCPU,FCPU;LPOPUP^%DASDP1;;	;%%Z2-X7
PU	;;,SCPU,FCPU;LPOPUP^%DASDP1;;	;%%Z2-X7
QUIT	;;,SCQT,FCQT;LQUIT^%DASDP1
REPAINT	;;,FCRP;LREPNT^%DASDP1
RP	;;,FCRP;LREPNT^%DASDP1
RESET	;;,FCRS;LRESET^%DASDP1
RS	;;,FCRS;LRESET^%DASDP1
REQUIRED	;;,FTRQ,DTRQ
REQ	;;,FTRQ,DTRQ
REVERSE	;;,FTRV
REV	;;,FTRV
SCROLL	;;,STSC,SCSC,FCSC;LSCROLL^%DASDP1
SC	;;,STSC,SCSC,FCSC;LSCROLL^%DASDP1
SET	;;,SCST,FCST,OCST;LSET^%DASDP1
SPACES	;;,FTSP,DTSP
SP	;;,FTSP,DTSP
SPACEL	;;,FTSL,DTSL
SL	;;,FTSL,DTSL
SPACET	;;,FTSA,DTSA
ST	;;,FTSA,DTSA ;	;%%Z3-PATCH 93/5/19
TABLE	;;,SCTB,FCTB,FATB,DATB;LTAB^%DASDP1
TB	;;,SCTB,FCTB,FATB,DATB;LTAB^%DASDP1
TABLEM	;;,SCTS,FCTS,FATS,DATS;LTAB^%DASDP1
TBM	;;,SCTS,FCTS,FATS,DATS;LTAB^%DASDP1
TABLEX	;;,SCTC,FCTC,FATC,DATC;LTAB^%DASDP1
TBX	;;,SCTC,FCTC,FATC,DATC;LTAB^%DASDP1
TALL	;;,FTTL
TL	;;,FTTL
TERM	;;,STTE,FTTE,DTTE;LTERM^%DASDP1  ;;%%Z2-R
TE	;;,STTE,FTTE,DTTE;LTERM^%DASDP1  ;;%%Z2-R
TIME	;;,SCTM,FCTM,FATM,DATM;LDATE^%DASDP1
TM	;;,SCTM,FCTM,FATM,DATM;LDATE^%DASDP1
TIMEOUT	;;,STTO,FTTO;LTIMO^%DASDP1
TO	;;,STTO,FTTO;LTIMO^%DASDP1
TSTART	;;,SCTT,FCTT;LTSTART^%DASDP1
TS	;;,SCTT,FCTT;LTSTART^%DASDP1
TCOMMIT	;;,SCTI,FCTI;LCOMMIT^%DASDP1
TC	;;,SCTI,FCTI;LCOMMIT^%DASDP1
UNDER	;;,FTUN
UN	;;,FTUN
UNLOCK	;;,SCUL,FCUL;LUNLCK^%DASDP1
ULK	;;,SCUL,FCUL;LUNLCK^%DASDP1
WIDE	;;,FTWD
WD	;;,FTWD
XECUTE	;;,SCXE,FCXE,FAXE,DAXE,OCXE;LXEC^%DASDP1
XE	;;,SCXE,FCXE,FAXE,DAXE,OCXE;LXEC^%DASDP1
XXXXX	;;
GEREF	G GEREF^%DASDP1
GLVAR	G GLVAR^%DASDP1
GNODE	G GNODE^%DASDP1
TEST	R !,"Screen, Field or Dataname: ",%X1
	I %X1="" K %X1,%X2,S,TYP,%TYP,%MSG,%RES,%OLD,SDFL,SDLL Q
	I ",S,F,D,"'[(","_%X1_",") W *7,"  S, F or D" G TEST
TEST1	W !,"ATtribute" W:%X1="D" " or" W:"FD"[%X1 " Validation" W:%X1'="D" " or ACtion"
	R ": ",%X2 G TEST:%X2=""
	I %X1="S"&(",AT,AC,"'[(","_%X2_",")) W *7,"  [AT or AC]" G TEST1
	E  I %X1="F"&(",AT,AC,V,"'[(","_%X2_",")) W *7,"  [AT, V or AC]" G TEST1
	E  I %X1="D"&(",AT,V,"'[(","_%X2_",")) W *7,"  [AT or V]" G TEST1
	S TYP=","_%X1_$S(%X2="V":"A",1:$E(%X2,2))
TEST2	R !,"Display parsed string? ",%X1
	I $L(%X1)'=1!("YN"'[%X1) W *7,"  Y or N" G TEST2
	S:%X1="Y" S="" K:%X1="N" S
TEST3	S %MSG="",%TYP=TYP,SDFL=1,SDLL=23
	R !,"%RES: ",%RES I %RES="" G TEST
	S %OLD=%RES D PLINE W:%MSG]"" !,%MSG I $D(S) W !,S S S=""
	I %RES'=%OLD W !,"New %RES: ",%RES
	G TEST3