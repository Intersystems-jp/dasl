ROUTINE %DAURCMP [Type=INT,LanguageMode=6]
%DAURCMP	;
	;;     Copyright ã‚¥ Digital Equipment Corporation.
	;;       1987 - 1992  All rights reserved.
BEGIN	;
	K %PRINT,%RPGP
	S %RPNM1=%PARM("RPNM1"),%RPNM2=%PARM("RPNM2")
	S %COUNT=0,%HEAD="REPORT SPECIFICATIONS",%ITEM=""
	S %ND1=^DARD(%RPNM1,"A"),%ND2=^DARD(%RPNM2,"A")
	F %PCE=1:1:3 S %DN1=$P(%ND1,";",%PCE),%DN2=$P(%ND2,";",%PCE) I %DN1'=%DN2 S %PROMPT=$P("Group;Description;Map",";",%PCE) D BUILDW:%PROMPT'="Map",BUILD:%PROMPT="Map"
	S %ND1=^DARD(%RPNM1,"H"),%ND2=^DARD(%RPNM2,"H")
	F %PCE=1:1:5 S %DN1=$P(%ND1,";",%PCE),%DN2=$P(%ND2,";",%PCE) I %DN1'=%DN2 S %PROMPT=$P("Version;Created By;Edited By;Edit Date;Compile Date;Edit Time",";",%PCE) D CVT:%PCE>3,BUILD
	S %ND1=^DARD(%RPNM1,"REP"),%ND2=^DARD(%RPNM2,"REP")
	F %PCE=1,2 S %DN1=$P(%ND1,";",%PCE),%DN2=$P(%ND2,";",%PCE) I %DN1'=%DN2 S %PROMPT=$P("Width;Length",";",%PCE) D BUILD
	F %SUB=1:1:3 S %ND1=^DARD(%RPNM1,"C","FW",%SUB),%ND2=^DARD(%RPNM2,"C","FW",%SUB) I %ND1'=%ND2 D COLLECT
	S %DN1=^DARD(%RPNM1,"C","S"),%DN2=^DARD(%RPNM2,"C","S")
	I %DN1'=%DN2 S %PROMPT="Sort by" D BUILDW
	F %SUB=%RPNM1,%RPNM2 S %RPGP="" F %DUM=0:0 S %RPGP=$O(^DARD(%SUB,"P",%RPGP)) Q:%RPGP=""  S %RPGP($S(%RPGP="D":"ID",1:%RPGP))=""
	S %PGR="" F %DUM=0:0 S %PGR=$O(%RPGP(%PGR)) Q:%PGR=""  D GROUP
KILL	K %DN1,%DN2,%ND1,%ND2,%RPNM1,%RPNM2,%PCE,%COUNT
	K %ITEM,%PROMPT,%SUB,%DUM,%LCNT,%RPLC,%RPGP,%PGR,%HEAD
	Q
CVT	;
	I %PCE=6 G CVTIME
	I %DN1]"" S %DTI=%DN1 D DTIX^%DAUDTTM S %DN1=%DTX
	I %DN2]"" S %DTI=%DN2 D DTIX^%DAUDTTM S %DN2=%DTX
	Q
CVTIME	;
	I %DN1]"" S %TMI=%DN1 D TMIX^%DAUDTTM S %DN1=%TMX
	I %DN2]"" S %TMI=%DN2 D TMIX^%DAUDTTM S %DN2=%TMX
	Q
GROUP	;
	S (%RPGP,%HEAD)=$S(%PGR="ID":"D",1:%PGR)
	S %HEAD=$S(%HEAD="D":"DATA",%HEAD["H":"HEADER",1:"TRAILER")_" LEVEL"_$S(%HEAD?1U1N:" "_$E(%HEAD,2),1:"")
	I '$D(^DARD(%RPNM1,"P",%RPGP)) S %DN2=%HEAD,%DN1="Not present" G BLDMG
	I '$D(^DARD(%RPNM2,"P",%RPGP)) S %DN1=%HEAD,%DN2="Not present" G BLDMG
	S %RPLC=0 F %LCNT=0:0 S %LCNT=$O(^DARD(%RPNM1,"P",%RPGP,"G",%LCNT)) Q:%LCNT=""  S (%ITEM,%RPLC)=%LCNT D GRP1
	F %LCNT=%RPLC:0 S %LCNT=$O(^DARD(%RPNM2,"P",%RPGP,"G",%LCNT)) Q:%LCNT=""  D GRP2
	Q
GRP1	;
	I '$D(^DARD(%RPNM2,"P",%RPGP,"G",%LCNT)) S %DN1=%LCNT,%DN2="Not present" G BUILDM
	S %DN2=^(%LCNT,"I"),%DN1=^DARD(%RPNM1,"P",%RPGP,"G",%LCNT,"I")
	I %DN1'=%DN2 S %PROMPT="Item" D BUILDW
	S %ND1=^DARD(%RPNM1,"P",%RPGP,"G",%LCNT,"E"),%ND2=^DARD(%RPNM2,"P",%RPGP,"G",%LCNT,"E")
	I %ND1=%ND2 Q
	F %PCE=1,2 S %DN1=$P(%ND1,";",%PCE),%DN2=$P(%ND2,";",%PCE) I %DN1'=%DN2 S %PROMPT=$P("Column;Format",";",%PCE) D BUILD
	Q
GRP2	;
	S %DN2=%LCNT,%DN1="Not present" D BUILDM
	Q
COLLECT	;
	F %PCE=1,2 S %DN1=$P(%ND1,";",%PCE),%DN2=$P(%ND2,";",%PCE) I %DN1'=%DN2 S %PROMPT=$P("For;With",";",%PCE)_" "_%SUB D BUILDW
	Q
BLDMG	;
	S %COUNT=%COUNT+1
	S %PRINT(%COUNT,"T1")=%DN1,%PRINT(%COUNT,"T2")=%DN2
	Q
BUILDM	;
	S %COUNT=%COUNT+1
	I %HEAD]"" S %PRINT(%COUNT,"T1")=%HEAD,%PRINT(%COUNT,"T2")=%HEAD,%HEAD=""
	S %PRINT(%COUNT,"L1")=%DN1,%PRINT(%COUNT,"L2")=%DN2
	Q
BUILD	;
	S %COUNT=%COUNT+1
	S %PRINT(%COUNT,"P1")=%PROMPT_": "_%DN1,%PRINT(%COUNT,"P2")=%PROMPT_": "_%DN2
	I %HEAD]"" S %PRINT(%COUNT,"T1")=%HEAD,%PRINT(%COUNT,"T2")=%HEAD,%HEAD=""
	I %ITEM]"" S %PRINT(%COUNT,"L1")=%ITEM,%PRINT(%COUNT,"L2")=%ITEM,%ITEM=""
	Q
BUILDW	;
	S %COUNT=%COUNT+1
	S %PRINT(%COUNT,"P1")=%PROMPT_":",%PRINT(%COUNT,"P2")=%PROMPT_":"
	I %HEAD]"" S %PRINT(%COUNT,"T1")=%HEAD,%PRINT(%COUNT,"T2")=%HEAD,%HEAD=""
	I %ITEM]"" S %PRINT(%COUNT,"L1")=%ITEM,%PRINT(%COUNT,"L2")=%ITEM,%ITEM=""
	S %PRINT(%COUNT,"V1",1)=$E(%DN1,1,$ZP(%DN1,35)),%PRINT(%COUNT,"V2",1)=$E(%DN2,1,$ZP(%DN2,35)) ;	;%%Z1-G
	I $ZL(%DN1)<36,$ZL(%DN2)<36 Q  ;	;%%Z1-G
	S %PRINT(%COUNT,"V1",2)=$E(%DN1,$ZP(%DN1,35)+1,$ZP(%DN1,70)),%PRINT(%COUNT,"V2",2)=$E(%DN2,$ZP(%DN2,35)+1,$ZP(%DN2,70)) ;	;%%Z1-G
	I $ZL(%DN1)>70!($ZL(%DN2)>70) S %PRINT(%COUNT,"V1",3)=$E(%DN1,$ZP(%DN1,70)+1,$ZP(%DN1,105)),%PRINT(%COUNT,"V2",3)=$E(%DN2,$ZP(%DN2,70)+1,$ZP(%DN2,105)) ;	;%%Z1-G
	Q