ROUTINE %DAQC [Type=INT,LanguageMode=6]
%DAQC	;
	;;     Copyright ゥ Digital Equipment Corporation.
	;;       1987 - 1992  All rights reserved.
INIT	;
	S:'$D(DB) DB=0 S %FLANG=^%DAMSG 
	S ($ZE,%MSG)="" 
	I '$D(%CALL) S %CALL=""
	I DB=1 S FILE=0
	E  D  G EXIT:%MSG]""
GETFILE	. S FILE="DSM_DASL_QD"_($H#7)_$P($H,",",2)_".TMP"
	. I $ZV["DSM for ULTRIX" S FILE=$ZC(%LOWCASE,FILE)
	. I $ZSE(FILE)]"" G GETFILE
	. I 'DB S $ZT="ERROPN^%DAQC"
	. C FILE O FILE:NEW:10
	. E  S %MSG=^%DAMSG(%FLANG,"QOPEN") 
	I 'DB S $ZT="ERRTOP^%DAQC"
	K %PK,DN,FR,PK,REF,TO N %VAR
	I ^DASY("PARM","DELIM")="" S %MSG=^%DAMSG(%FLANG,"SYSERR") G EXIT
	S FLAG("DELIM")=$$DELIM^%DASDUTL(^DASY("PARM","DELIM"))
	S FLAG("DASL")=$$MODE^%DAZCALL
	I %CALL=1 S %RTNM=%QROU
	I %CALL'=1 S %NAME=QUNM D QUERY^%DANAME S %RTNM=$E(%PROGRAM,2,99)
	I 'DB,$T(^@%RTNM)'="",'$D(FLAG("FORCE_COMPILE")) G EXIT
	I %RTNM="" S %MSG=^%DASMG(%FLANG,"SYSERR") G EXIT
	S %VAR=$S(FLAG("DASL"):"%",1:"")
	S %VAR=%VAR_"Qq"
	S %VAR=%VAR_";0;0" 
	D NOW^%DAUDTTM
	S %VC=0,%LBCNT=25,$P(XX,"=",68)=""
COMPILE	;
	N KILL 
	D ^%DAQPAR:%CALL="" D RUN^%DAQPAR:%CALL'="" G:%MSG'="" EXIT
BREAK	D ^%DAQC1 G:%MSG'="" EXIT
	D ^%DAQC2 G:%MSG'="" EXIT
	D ^%DAQC3 G:%MSG'="" EXIT
	D ^%DAQC4 G:%MSG'="" EXIT 
	D ^%DAQC6 G:%MSG'="" EXIT
	D ^%DAQC7 G:%MSG'="" EXIT
	D ^%DAQC9 G:%MSG'="" EXIT
	D WRITE
EXIT	;
	U 0:(CTRAP="") S %MSG=$S(%MSG["CTRAP":"コンパイル異常終了。",1:%MSG) ;	;%%Z2-T
	S:'$D(%MSG) %MSG=""
	I $D(FILE) Q:FILE=0  C FILE:DEL
	K ARG,%CF,%DDNM,%DDREF,%DDXTRCT,%DES,%DM,%DN,%DNX,%DTI,%DTX,%ECNT
	K %FC,%FG,%FITM,%FLS,%FNC,%FNU,%GLOBAL,%I,%JJ,%JX,%K,%KEY,%KEYS
	K %LBCNT,%LS,%LS1,%MODE,%NG,%NXT,%PK,%QGB,%REF,%RTNM,%SP,%SW
	K %TMI,%TMX,%VAR,%VC,%Z,%Z1,LENFLG,SUB,VAR,%C,%Q,%D
	K ASN,ATOM,BEG,CM,CNT,COM,COND,CV,DV,DEP,DN,END,FILE,FITM,FLAG
	K FNAME,FNC,FNM,FOUT,FNUM,GARB,I,J,JJ,K,KEY,L,LB,LEN,LN,NOD,NX
	K NUMBERIC,ODX1,OPERATOR,OPT,OT,OUT,PIECE,PK,POS,PP,QF,QFCNT,CPT
	K REF,RF,SEMI,SK,SP,STR,STRING,SX,TAG,TYPE,V,V1,VE,S,STRX
	K X,XDEP,XKEY,XP,XREF,XTYP,XX,NUMFLG,O,P,OPER
	D KILLALL^%DAQPAR
	Q
WRITE	Q:FILE=0  
	Q:%CALL
	S $P(^DAQD(QUNM,"1A"),";",3)=+$H
	S $P(^DAQD(QUNM,"1A"),";",4)=+$P($H,",",2)
	Q
ERROPN	;
	S %MSG=^%DAMSG(%FLANG,"QOPEN")
	I $D(FILE) C FILE:DEL
	G EXIT
ERRTOP	;
	S:$ZE["llocation" %MSG=^%DAMSG(%FLANG,"QALLO")
	S %MSG="" S %MSG="Error - "_$S($ZE]"":$ZE,1:^%DAMSG(%FLANG,"QCOMP"))
	G EXIT
TEST	;
	S (QUG1,QUG2)=""
	R !,"Query: ",QUNM Q:QUNM=""  
	I '$D(^DAQD(QUNM)) W *7," Not defined." G TEST
	S %NAME=QUNM D QUERY^%DANAME W ?20,$P(^DAQD(QUNM,"1A"),";"),!
	S DB=1 D INIT
	I %MSG]"" W $C(27),"[1m",%MSG,$C(27),"[0m",!!
	Q